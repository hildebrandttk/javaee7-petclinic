group = 'tk.hildebrandt.javaee7.petclinic'

buildscript {
   repositories {
      jcenter()
      mavenLocal()
   }
   dependencies {
      classpath files("$rootDir/gradle/se.transmode.gradle.gradle-docker-1.2p15.jar")
      classpath 'com.google.guava:guava:17.0'
      classpath'com.github.docker-java:docker-java:0.9.0'
   }
}

ext {
   junitVersion = '4.12'
   gebVersion = '1.1.1'
   groovyVersion = '2.1.2'
   arquillianVersion = '1.1.8.Final'
   arquillianCoreVersion = '1.1.11.Final'
   arquillianWarpVersion = '1.0.0.Alpha8'
   arquillianPersistenceVersion = '1.0.0.Final-SNAPSHOT'
   seleniumVersion = '3.4.0'
   slfVersion = '1.7.7'
   phantomjsDriverVersion = '1.2.1'
   shrinkwrapShrinkwrapVersion = '1.2.1'
   webJarsBootstrapVersion = '3.1.1'
   webJarsJQueryVersion = '2.1.1'
   richfacesVersion = '4.5.5.Final'
   wildflyVersion = '8.2.1.Final'
   cucumberVersion = '1.1.5'
   grapheneVersion = '2.0.3.Final'
   droneVersion = '2.0.1.Final'
   httpClientVersion = '4.3.4'
   httpCoreVersion = '4.3.2'
   httpMimeVersion = '4.3.5'
   commonsIoVersion = '2.4'
   hamcrestVersion = '1.3'
   dockerRegistry = 'registry:5000/'
   jdk8UpdateVersion = '131'
   jdk8Version = "8u${jdk8UpdateVersion}"
   javaContainerVersion = "${jdk8Version}-1"
   baseContainerVersion = '1.0'
   mysqlContainerVersion = '1.0'
   oracleXeContainerVersion = '11.2.0-2-1.0'
   petclinicVersion = '1.4.0'
}


subprojects {
   configurations.all {
      exclude(module: 'selenium-server')
      exclude group: 'org.seleniumhq.selenium', module:  "htmlunit-driver"
      exclude group: 'org.seleniumhq.selenium', module:  "selenium-htmlunit-driver"
      exclude(module: 'shrinkwrap-descriptors-api')

      resolutionStrategy {
//         failOnVersionConflict()
         force 'org.apache.avro:avro:1.7.6'
         force "org.slf4j:slf4j-api:${slfVersion}"
         force "commons-io:commons-io:${commonsIoVersion}"
         force "org.apache.httpcomponents:httpclient:${httpClientVersion}"
         force "org.jboss.arquillian.graphene:graphene-webdriver:${grapheneVersion}"
         force "org.apache.httpcomponents:httpcore:${httpCoreVersion}"
         force "org.apache.httpcomponents:httpmime:${httpMimeVersion}"
         eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.seleniumhq.selenium') {
               details.useVersion seleniumVersion
            }
            if (details.requested.group == 'org.jboss.arquillian.extension' && details.requested.name.contains('drone')) {
               details.useVersion droneVersion
            }
            if (details.requested.version == '1.1.2.Final-wildfly-1') {
               details.useVersion arquillianVersion
            }
         }
      }
   }

   repositories {
      mavenLocal()
      jcenter()
      mavenCentral()
      maven {
         url "http://repo.bodar.com"
      }
      maven {
         url 'http://central.maven.org/maven2/'
      }
   }
}
