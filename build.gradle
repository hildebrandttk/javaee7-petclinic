group = 'tk.hildebrandt.javaee7.petclinic'

ext {
   junitVersion = '4.11'
   gebVersion = '0.9.3'
   groovyVersion = '2.1.2'
   arquillianVersion = '1.1.4.Final'
   arquillianCoreVersion = '1.1.4.Final'
   arquillianWarpVersion = '1.0.0.Alpha7'
   seleniumVersion = '2.43.1'
   slfVersion = '1.7.7'
   phantomjsDriverVersion = '1.0.4'
   shrinkwrapShrinkwrapVersion = '1.2.1'
   webJarsBootstrapVersion = '3.1.1'
   webJarsJQueryVersion = '2.1.1'
   richfacesVersion = '5.0.0-SNAPSHOT'
   wildflyVersion = '8.1.0.Final'
   cucumberVersion = '1.1.5'
   grapheneVersion = '2.0.3.Final'
   droneVersion = '1.3.1.Final'
   httpClientVersion = '4.3.4'
   httpCoreVersion = '4.3.2'
   httpMimeVersion = '4.3.5'
   commonsIoVersion = '2.4'
}

subprojects {
   configurations.all {
      exclude(module: 'selenium-server')

      resolutionStrategy {
//         failOnVersionConflict()
         force 'org.apache.avro:avro:1.7.6'
         force "org.slf4j:slf4j-api:${slfVersion}"
         force "commons-io:commons-io:${commonsIoVersion}"
         force "org.apache.httpcomponents:httpclient:${httpClientVersion}"
         force "org.apache.httpcomponents:httpcore:${httpCoreVersion}"
         force "org.apache.httpcomponents:httpmime:${httpMimeVersion}"
         eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.seleniumhq.selenium') {
               details.useVersion seleniumVersion
            }
            if (details.requested.group == 'org.jboss.arquillian.extension' && details.requested.name.contains('drone')) {
               details.useVersion droneVersion
            }
         }
      }
   }

   repositories {
      mavenCentral()
      mavenLocal()
      maven {
         url "http://repo.bodar.com"
      }
   }
}
